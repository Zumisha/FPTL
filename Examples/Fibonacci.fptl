Scheme Fibonacci
{
	N = [1];	
	
	@ = ((N * N.toInt).sub * 0).nequal ->
			(("Ошибка: аргумент должен быть целым числом! x = " * N.toString).cat * "\n").cat.print,
			StartFib.print;
	
	StartFib = (N * 0).equal -> 0.0,
					(N * 0).greater ->
						(N * 0.0 * 1.0).Fib,
							(((N * -1).mul * 0.0 * 1.0).Fib * evenSign).mul;
							
	evenSign = ((N * 2).mod * 0).equal -> -1, 1;
	
	F1 = [2];
	F2 = [3];
	
	Fib = (N * 1).equal -> F2, ((N * 1).sub * F2 * (F1 * F2).add).Fib;
}

Application
%Fibonacci(37)